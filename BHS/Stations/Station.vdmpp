class Station
types

public StationID = token

values
-- TODO Define values here

instance variables

private name : seq1 of char;
private id : StationID;
protected segments : set of Segment;
protected currentTote : [Tote] := nil;

operations


public Station : StationID * seq1 of char ==> Station
Station(pId, pName) ==
  (
    id := pId;
    name := pName;
    segments := {};
  );

pure public CanProcess : () ==> bool
CanProcess() == is subclass responsibility;

public Process : Tote ==> ()
Process(tote) == is subclass responsibility
pre CanProcess(); -- TODO: Are pre-conditions inherited?

public Step : () ==> ()
Step() == is subclass responsibility;

pure public GetId : () ==> StationID
GetId() == return id;

pure public GetName : () ==> seq of char
GetName() == return name;

public AddSegment : Segment ==> ()
AddSegment(pSegment) == segments := segments union { pSegment }
pre pSegment.HasStation(self);

public pure GetCurrentTote : () ==> [Tote]
GetCurrentTote() ==
  return currentTote;

functions

protected GetInSegments : StationID * set of Segment -> set of Segment
GetInSegments(pCurrentStationId, pSegments) ==
  { s | s in set pSegments & s.IsTargetStation(pCurrentStationId) };

protected GetOutSegments : StationID * set of Segment -> set of Segment
GetOutSegments(pCurrentStationId, pSegments) ==
  { s | s in set pSegments & s.IsSourceStation(pCurrentStationId) };

end Station
