class Station is subclass of Global

instance variables

protected id : StationID;
protected name : seq1 of char;
protected currentTote : [Tote] := nil;
protected segments : set of Segment;
inv forall segment in set segments & segment.HasStation(id);

operations

public Station : StationID * seq1 of char ==> Station
Station(pId, pName) ==
  atomic
  (
    id := pId;
    name := pName;
    segments := {};
  );

public pure CanProcess : () ==> bool
CanProcess() == return currentTote = nil;

public Process : Tote ==> ()
Process(pTote) == currentTote := pTote
pre CanProcess();

public Step : () ==> ()
Step() == is subclass responsibility;

public pure GetId : () ==> StationID
GetId() == return id;

public pure GetName : () ==> seq of char
GetName() == return name;

public AddSegment : Segment ==> ()
AddSegment(pSegment) == segments := segments union { pSegment }
pre pSegment.HasStation(self)
post self.HasSegment(pSegment);

public pure GetCurrentTote : () ==> [Tote]
GetCurrentTote() ==
  return currentTote;

public pure Equals: Station ==> bool
Equals(pOtherStation) ==
  return id = pOtherStation.id;


public pure FindInSegments : () ==> set of Segment
FindInSegments() ==
  return { seg | seg in set segments & seg.IsTargetStation(self) };


public pure FindOutSegments : () ==> set of Segment
FindOutSegments() ==
  return { seg | seg in set segments & seg.IsSourceStation(self) };

public pure HasSegment : Segment ==> bool
HasSegment(pSegment) == return pSegment in set segments;

functions

protected GetInSegments : StationID * set of Segment -> set of Segment
GetInSegments(pCurrentStationId, pSegments) ==
  { s | s in set pSegments & s.IsTargetStation(pCurrentStationId) };

protected GetOutSegments : StationID * set of Segment -> set of Segment
GetOutSegments(pCurrentStationId, pSegments) ==
  { s | s in set pSegments & s.IsSourceStation(pCurrentStationId) };

end Station
