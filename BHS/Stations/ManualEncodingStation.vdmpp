class ManualEncodingStation is subclass of Station

values

DefaultNumberOfStepsForProcessing = 2

instance variables

numOfStepsForProcessing : nat1 := DefaultNumberOfStepsForProcessing;
stepCounter : nat := 0;

operations

public ManualEncodingStation : StationID * seq of char * nat1 ==> ManualEncodingStation
ManualEncodingStation(pId, pName, pNumSteps) ==
  (
    numOfStepsForProcessing := pNumSteps;
    Station(pId, pName);
  );

public ManualEncodingStation : StationID * seq of char ==> ManualEncodingStation
ManualEncodingStation(pId, pName) ==
  ManualEncodingStation(pId, pName, DefaultNumberOfStepsForProcessing);

public pure CanProcess : () ==> bool
CanProcess() == return currentTote = nil
pre card segments >= 2 and card FindOutSegments() = 1;

public Process : Tote ==> ()
Process(pTote) ==
  (
    currentTote := pTote;
    stepCounter := numOfStepsForProcessing;
  )
pre pTote.GetBaggage() <> nil and
    pTote.GetBaggage().HasAnnotation(<BadTag>);

public Step : () ==> ()
Step() ==
  (
    if currentTote <> nil
    then
      if stepCounter <> 0
      then
        stepCounter := stepCounter - 1
      else
        let outSegment = FindOutSegment(GetId(), segments)
        in
          if outSegment.CanProcess(currentTote)
          then
            (
              currentTote.GetBaggage().RemoveAnnotation(<BadTag>);
              outSegment.Process(currentTote);
              currentTote := nil;
            );
  );

functions

private FindOutSegment : Station`StationID * set of Segment -> Segment
FindOutSegment(pCurrentStationId, pSegments) ==
  let outSegment in set
    { s | s in set pSegments & s.IsSourceStation(pCurrentStationId) }
  in
    outSegment;

end ManualEncodingStation
